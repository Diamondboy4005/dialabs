plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

base.archivesName = project.archives_base_name
version = "$project.version+${libs.versions.minecraft.get()}"
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases'
	}

	maven {
		url 'https://jitpack.io'
	}

	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
}

loom {
	accessWidenerPath = file("src/main/resources/dialabs.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft libs.minecraft
	mappings variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }

	modImplementation libs.quilt.loader // my favourite part was when he said its quiltin time and then quilted all over the place

	// QFAPI
	modImplementation libs.quilted.fabric.api

	// CCA
	include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_ver}")
	include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_ver}")

	// Fabric ASM
	include modImplementation("com.github.Chocohead:Fabric-ASM:${project.asm_version}")

	// LibGenetics
	include modImplementation("com.github.diamonddevv:libgenetics:${project.libgenetics_version}")

	// Simple Trims
	include modImplementation("com.github.diamonddevv:simple-trims:${project.simple_trims_version}");

	// MixinExtras
	include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}")))
}

processResources {
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
